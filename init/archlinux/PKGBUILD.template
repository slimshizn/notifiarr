# Maintainer: David Newhall II <captain@golift.io>

pkgname='notifiarr-bin'
appname='notifiarr'
pkgver='{{VERSION}}'
pkgrel='{{Iter}}'
pkgdesc='{{Desc}}'
arch=('x86_64' 'armhf' 'armv7h' 'aarch64' 'i686' 'pentium4')
url='https://notifiarr.com'
license=('MIT')
backup=("etc/${appname}/${appname}.conf")

source=("${pkgname}-${pkgver}.tar.gz::https://codeload.github.com/Notifiarr/notifiarr/tar.gz/refs/tags/v${pkgver}")
sha512sums=('{{SHA}}')

source_x86_64=("${pkgname}-${pkgver}.x86_64.gz::https://github.com/Notifiarr/notifiarr/releases/download/v${pkgver}/notifiarr.amd64.linux.gz")
source_armhf=("${pkgname}-${pkgver}.armhf.gz::https://github.com/Notifiarr/notifiarr/releases/download/v${pkgver}/notifiarr.arm.linux.gz")
source_armv7h=("${pkgname}-${pkgver}.armv7h.gz::https://github.com/Notifiarr/notifiarr/releases/download/v${pkgver}/notifiarr.arm.linux.gz")
source_aarch64=("${pkgname}-${pkgver}.aarch64.gz::https://github.com/Notifiarr/notifiarr/releases/download/v${pkgver}/notifiarr.arm64.linux.gz")
source_i686=("${pkgname}-${pkgver}.i686.gz::https://github.com/Notifiarr/notifiarr/releases/download/v${pkgver}/notifiarr.386.linux.gz")
source_pentium4=("${pkgname}-${pkgver}.pentium4.gz::https://github.com/Notifiarr/notifiarr/releases/download/v${pkgver}/notifiarr.386.linux.gz")
sha512sums_x86_64=('{{SHA_X64}}')
sha512sums_armhf=('{{SHA_ARMHF}}')
sha512sums_armv7h=('{{SHA_ARMHF}}')
sha512sums_aarch64=('{{SHA_ARCH64}}')
sha512sums_i686=('{{SHA_386}}')
sha512sums_pentium4=('{{SHA_386}}')

build() {
  cd "${appname}-${pkgver}"
  go run github.com/davidnewhall/md2roff@v0.0.1 --manual "${appname}" --version "${pkgver}" --date "${DATE}" README.md
  go run github.com/davidnewhall/md2roff@v0.0.1 --manual "${appname}" --version "${pkgver}" --date "${DATE}" examples/MANUAL.md
  gzip -9 examples/MANUAL
  mv examples/MANUAL.gz "${appname}.1.gz"
}

package() {  
  # Binary.
  install -D -m 755 "${pkgname}-${pkgver}.${CARCH}" "${pkgdir}/usr/bin/${appname}"

  cd "${appname}-${pkgver}"

  # Directories.
  install -d -m 775 "${pkgdir}/usr/share/"{licenses,doc}"/${appname}" "${pkgdir}/usr/share/applications" "${pkgdir}/etc/${appname}"

  # Install configuration file(s).
  install -D -m 644 "examples/${appname}.conf.example" "${pkgdir}/etc/${appname}/${appname}.conf"
  install -D -m 644 "examples/${appname}.conf.example" "${pkgdir}/etc/${appname}/${appname}.conf.example"

  # License, documentation, logo icon, manual.
  install -D -m 644 LICENSE "${pkgdir}/usr/share/licenses/${appname}/LICENSE"
  install -D -m 644 ./*.html examples/* pkg/bindata/files/images/logo/${appname}.png "${pkgdir}/usr/share/doc/${appname}/"
  install -D -m 644 "${appname}.1.gz" "${pkgdir}/usr/share/man/man1/${appname}.1.gz"

  # X11 Desktop File.
  install -D -m 755 init/linux/deb/usr/share/applications/${appname}.desktop "${pkgdir}/usr/share/applications/"

  # Install the systemd service unit and system user account.
  install -D -m 644 "init/systemd/${appname}.service" "${pkgdir}/usr/lib/systemd/system/${appname}.service"
  echo "u ${appname} - \"${appname} daemon\"" > "${appname}.sysusers"
  install -D -m 644 "${appname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${appname}.conf"
  rm "${appname}.sysusers"
}
